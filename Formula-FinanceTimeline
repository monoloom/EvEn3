/* CE-Invoices - Info v1.0 */

lets(
    /* Some basic definitions */
    type, prop("Type"),
    status, prop("Status"),
    untilDue, prop("Days Until Due"),
    beginDate, prop("Beginning Date"),
    delta, prop("Date Delta"),

    /* Returns an integer from Payment Terms */
    terms, let(n, replaceAll(prop("Payment Terms"), "[^0-9]").toNumber(), empty(n) ? 0 : n),

    /* Calculates days until the Beginning Date */
    untilBegin, (
        status == "Created" or status == "Expected" ?
            (lets(
                
                today, parseDate(formatDate(now(), "YYYYMMDD")),
                begin, parseDate(formatDate(prop("Beginning Date"), "YYYYMMDD")),

                /* Calculate how many days until beginnning */
                dateBetween(begin, today, "days")
            )) : (
                "Error: \nStatus isn't: Created."
            )

    ),

    /* Reused logic */
    printDelta, (
        "\n (" + delta + (
            delta == 1 ? " day" : " days"
        ) + " total.)"
    ),

    printPaid, (
        ifs(
            delta == (terms - 1), (
                "ðŸŸ¢ Paid one day early." + printDelta
            ),

            delta < terms, (
                "ðŸŸ¢ Paid " + (terms - delta) + " days early." + printDelta
            ),

            delta == terms, (
                "ðŸŸ¢ Paid on time." + printDelta
            ),

            delta == (terms + 1), (
                "ðŸŸ¡ Paid one day late." + printDelta
            ),

            delta > terms, (
                "ðŸŸ¡ Paid " + (delta - terms) + " days late." + printDelta
            ),

            "Error: \nMissing date(s)."
        )

    ),

    printVoid, (
        /* type == Void logic */
        delta > 0 ? (
            delta == 1 ? ("âŒ Voided after one day.") : ("âŒ Voided after " + delta + " days.")
        ) : ("âŒ Voided.")
    ),

    printInvalid, ("Invalid status for type:" + "\n" + type + "."),

/* Info Parser */
    type ? (
        ifs(
            type == "Receivable", (
                /* Type is Receivable */
                ifs(
                    status == "Created", ( /* Status is Created */
                        ifs(
                            untilBegin > 1, (
                                "ðŸŸ¢ Created: Send in " + untilBegin + " days."
                            ),

                            untilBegin == 1, (
                                "ðŸŸ¢ Created: Send tomorrow."
                            ),

                            untilBegin == 0, (
                                "ðŸŸ¡ Created: Send today."
                            ),

                            "ðŸ”´ Created: Not yet sent."
                        )
                    ),

                    status == "Expected", (printInvalid), /* Status is Expected */

                    status == "Sent", ( /* Status is Sent */
                        ifs(
                            untilDue > 1, (
                                "ðŸŸ¢ Sent: Due in " + untilDue + " days."
                            ),

                            untilDue == 1, (
                                "ðŸŸ¢ Sent: Due tomorrow."
                            ),

                            untilDue == 0, (
                                "ðŸŸ¡ Sent: Due today."
                            ),

                            untilDue == -1, (
                                "ðŸ”´ Sent: Due yesterday."
                            ),
                            untilDue < -1, (
                                "ðŸ”´ Sent: Due " + abs(untilDue) + " days ago."
                            ),
                            "ðŸ”´ Sent: No Beginning Date."
                            
                        )
                    ),

                    status == "Received", (printInvalid), /* Status is Received */

                    status == "Processing", ( /* Status is Processing */
                        ifs(
                            untilDue > 1, (
                                "ðŸŸ¢ Processing: Due in " + untilDue + " days."
                            ),

                            untilDue == 1, (
                                "ðŸŸ¢ Processing: Due tomorrow."
                            ),

                            untilDue == 0, (
                                "ðŸŸ¡ Processing: Due today."
                            ),

                            untilDue == -1, (
                                "ðŸ”´ Processing: Due yesterday."
                            ),
                            untilDue < -1, (
                                "ðŸ”´ Processing: Due " + abs(untilDue) + " days ago."
                            ),
                            "ðŸ”´ Processing: No Beginning Date."
                            
                        )
                    ),

                    status == "Paid", (printPaid), /* Status is Paid */

                    status == "Void", (printVoid), /* Status is Void */

                    "Error: \nUnknown Status." /* Status is something else */

                )

            ),

            type == "Payable", (
                /* Type is Payable */
                ifs(
                    status == "Created", (printInvalid), /* Status is Created */

                    status == "Expected", ( /* Status is Expected */
                        ifs(
                            untilBegin > 1, (
                                "ðŸŸ¢ Expected in " + untilBegin + " days."
                            ),

                            untilBegin == 1, (
                                "ðŸŸ¢ Expected: tomorrow."
                            ),

                            untilBegin == 0, (
                                "ðŸŸ¡ Expected: today."
                            ),

                            untilBegin == -1, (
                                "ðŸ”´ Expected: yesterday."
                            ),
                            untilBegin < -1, (
                                "ðŸ”´ Expected: " + abs(untilBegin) + " days ago."
                            ),
                            "ðŸŸ¢ Expected"
                            
                        )
                    ),

                    status == "Sent", (printInvalid), /* Status is Sent */

                    status == "Received", ( /* Status is Received */
                        ifs(
                            untilDue > 1, (
                                "ðŸŸ¢ Received: Due in " + untilDue + " days."
                            ),

                            untilDue == 1, (
                                "ðŸŸ¢ Received: Due tomorrow."
                            ),

                            untilDue == 0, (
                                "ðŸŸ¡ Received: Due today."
                            ),

                            untilDue == -1, (
                                "ðŸ”´ Received: Due yesterday."
                            ),
                            untilDue < -1, (
                                "ðŸ”´ Received: Due " + abs(untilDue) + " days ago."
                            ),
                            "ðŸ”´ Received: No Beginning Date."
                            
                        )
                    ),

                    status == "Processing", ( /* Status is Processing */
                        ifs(
                            untilDue > 1, (
                                "ðŸŸ¢ Processing: Due in " + untilDue + " days."
                            ),

                            untilDue == 1, (
                                "ðŸŸ¢ Processing: Due tomorrow."
                            ),

                            untilDue == 0, (
                                "ðŸŸ¡ Processing: Due today."
                            ),

                            untilDue == -1, (
                                "ðŸ”´ Processing: Due yesterday."
                            ),
                            untilDue < -1, (
                                "ðŸ”´ Processing: Due " + abs(untilDue) + " days ago."
                            ),
                            "ðŸ”´ Processing: No Beginning Date."
                            
                        )
                    ),

                    status == "Paid", (printPaid), /* Status is Paid */

                    status == "Void", (printVoid), /* Status is Void */

                    "Error: \nUnknown Status." /* Status is something else */

                )
            ),

            "Error: \nUnknown Type."
        )
    ) : "Error: \nNo Type."
)